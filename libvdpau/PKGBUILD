pkgname=libvdpau
pkgver=1.5
pkgrel=3
pkgdesc='Nvidia VDPAU library'
arch=(x86_64)
url=https://www.freedesktop.org/wiki/Software/VDPAU/
license=(custom)
depends=(
  libxext
)
makedepends=(
  git
  meson
  xorgproto
)
optdepends=(
  'libvdpau-va-gl: driver using VAAPI'
  'mesa-vdpau: driver for Mesa'
  'nvidia-utils: driver for NVIDIA'
)
provides=(
  libvdpau.so
)
_tag=b40ac3c8f6cac061ddd5ed70c8305238f97a1b25
source=(git+https://gitlab.freedesktop.org/vdpau/libvdpau.git#tag=${_tag})
b2sums=('SKIP')


_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

pkgver() {

  cd "${srcdir:?}/libvdpau" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libvdpau directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {

  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong/}
  LDFLAGS=${LDFLAGS/,-z,now/}
  
  meson setup libvdpau build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  ninja -C build

}

check() {

  ninja -C build test || true

}

package() {

  DESTDIR="${pkgdir:?}" ninja -C build install
  install -Dm 644 libvdpau/COPYING -t "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
