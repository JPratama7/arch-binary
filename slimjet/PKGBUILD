# Maintainer: dalto <dalto[at]fastmail.com>
# Contributor: eagleeyetom <eagleeyetom@gmail.com>
# Original Maintainer: raininja <dandenkijin@gmail.com>
pkgname=slimjet
pkgver=35.0.3.0
pkgrel=1
pkgdesc="Fast, smart and powerful browser based on Blink"
arch=('i686' 'x86_64')
url="http://www.slimjet.com"
license=('custom:freeware')
depends=('gtk3' 'harfbuzz-icu' 'libxss' 'nss' 'ttf-font')
optdepends=('kdialog: needed for file dialogs in KDE' 'ttf-liberation: fix fonts for some PDFs')
options=('!emptydirs' '!strip')
_channel=beta
_url=https://www.slimjetbrowser.com
source=(
    "${_url}/${_channel}/${pkgname}_amd64.deb"
    "${_url}/${_channel}/${pkgname}_amd64.tar.xz"
    'LICENSE'
    'slimjet.desktop'
    )
md5sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    )

prepare() {
    bsdtar -xf data.tar.xz
    rm -r opt/slimjet
    cp -r slimjet opt/slimjet
    rm usr/share/applications/slimjet.desktop
    cp slimjet.desktop usr/share/applications/slimjet.desktop
}

package() {
    cp --parents -a {opt,usr/bin,usr/share} "${pkgdir}"

    # install license
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install  "/${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # install icons
    for i in 48x48 128x128; do
        install -Dm644 "${pkgdir}"/opt/slimjet/product_logo_${i/x*}.png \
                       "${pkgdir}"/usr/share/icons/hicolor/${i}/apps/flashpeak-slimjet.png
    done
}
